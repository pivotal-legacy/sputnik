group = "com.sputnik"
version = "0.1-SNAPSHOT"
description = """Sputnik"""

ext {
    springVersion = "4.0.6.RELEASE"
    springBootVersion = "1.1.4.RELEASE"
    groovyVersion = "2.3.3"
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.4.RELEASE")
    }
}

apply plugin: "java"
apply plugin: "spring-boot"
apply plugin: "war"

sourceCompatibility = 1.8
targetCompatibility = 1.8

task(deploy, dependsOn: build) << {
    def warFile = "build/libs/sputnik-" + version + ".war"
    def buildpack = "https://github.com/spring-io/java-buildpack"
    def appName = "sputnik"

    println "running: cf push $appName -p $warFile -b $buildpack"

    def builder = new ProcessBuilder("cf", "push", appName, "-p", warFile, "-b", buildpack)
    builder.inheritIO()

    def proc = builder.start()
    proc.waitFor()
}

war {
    configurations {
        providedRuntime
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // Spring Boot
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"

    // Groovy Templates
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile "org.codehaus.groovy:groovy-templates:$groovyVersion"

    // Persistence
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "com.h2database:h2"
    compile "postgresql:postgresql:9.0-801.jdbc4"

    // Security
    compile "org.springframework.security:spring-security-config"
    compile "org.springframework.security:spring-security-taglibs"
    compile "org.springframework.security:spring-security-web"

    // Social
    compile "org.springframework.social:spring-social-core:1.1.0.RELEASE"
    compile "org.springframework.social:spring-social-web:1.1.0.RELEASE"
    compile "org.springframework.social:spring-social-config:1.1.0.RELEASE"
    compile "org.springframework.social:spring-social-security:1.1.0.RELEASE"
    compile name: "spring-social-strava-1.0.0.BUILD-SNAPSHOT"

    // Email
    compile 'com.sendgrid:sendgrid-java:1.2.1'

    // Logging
    compile "org.slf4j:slf4j-api:1.7.7"

    // Payments
    compile "com.stripe:stripe-java:1.19.1"

    // Testing
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
}
